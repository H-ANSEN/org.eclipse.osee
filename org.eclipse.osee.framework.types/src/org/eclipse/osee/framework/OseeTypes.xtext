grammar org.eclipse.osee.framework.OseeTypes with org.eclipse.xtext.common.Terminals

generate oseeTypes "http://www.eclipse.org/osee/framework/OseeTypes"


OseeTypeModel :
	(imports+=Import)*
	(types+=OseeType)*;

Import :
	'import' importURI=STRING;
	
terminal WHOLE_NUM_STR: ('0'..'9')+;

NAME_REFERENCE: STRING;
QUALIFIED_NAME : ID ('.' ID)*;

OseeType:
	ArtifactType | RelationType | AttributeType | OseeEnumType;

ArtifactType:
	(abstract?='abstract')? 'artifactType' name=NAME_REFERENCE ('extends' superArtifactTypes+=[ArtifactType|NAME_REFERENCE] (',' superArtifactTypes+=[ArtifactType|NAME_REFERENCE])*)? ('overrides' override=[ArtifactType|NAME_REFERENCE])? '{'
		typeGuid=STRING?
	    (validAttributeTypes+=AttributeTypeRef)*
	'}';

AttributeTypeRef:
	'attribute' validAttributeType=[AttributeType|NAME_REFERENCE] ('branchGuid' branchGuid=STRING)?;
	
AttributeType:
		typeGuid=STRING?
		'attributeType' name=NAME_REFERENCE ('extends' baseAttributeType=AttributeBaseType) ('overrides' override=[AttributeType|NAME_REFERENCE])? '{'
		'dataProvider' dataProvider = ('DefaultAttributeDataProvider' | 'UriAttributeDataProvider' | 'MappedAttributeDataProvider' | QUALIFIED_NAME)
		'min' min=WHOLE_NUM_STR
		'max' max=(WHOLE_NUM_STR | 'unlimited')
		('taggerId' taggerId = ('DefaultAttributeTaggerProvider' | QUALIFIED_NAME))?
		('enumType' enumType = [OseeEnumType|NAME_REFERENCE])?
		('description' description = STRING)?
		('defaultValue' defaultValue = STRING)?
		('fileExtension' fileExtension = STRING)?
	'}';

AttributeBaseType:
	'BooleanAttribute' | 'CompressedContentAttribute' | 'DateAttribute' | 'EnumeratedAttribute' | 'FloatingPointAttribute' | 'IntegerAttribute' | 'JavaObjectAttribute' | 'StringAttribute' | 'WordAttribute' | QUALIFIED_NAME;

OseeEnumType:
	'oseeEnumType' name=NAME_REFERENCE ('overrides' override=[OseeEnumType|NAME_REFERENCE])? '{'
		typeGuid=STRING?
		(enumEntries+=OseeEnumEntry)*
	'}';

OseeEnumEntry:
	'entry' name=STRING (ordinal=WHOLE_NUM_STR)?;

enum RelationMultiplicityEnum :  
	ONE_TO_ONE | ONE_TO_MANY | MANY_TO_ONE | MANY_TO_MANY
;

RelationType:
	'relationType' name=NAME_REFERENCE ('overrides' override=[RelationType|NAME_REFERENCE])? '{'
		typeGuid=STRING?
		'sideAName' sideAName=STRING
		'sideAArtifactType' sideAArtifactType=[ArtifactType|NAME_REFERENCE]
		'sideBName' sideBName=STRING
		'sideBArtifactType' sideBArtifactType=[ArtifactType|NAME_REFERENCE]
		'defaultOrderType' defaultOrderType=RelationOrderType
		'multiplicity' multiplicity=RelationMultiplicityEnum
	'}';
	
RelationOrderType:
	'Lexicographical_Ascending' | 'Lexicographical_Descending' | 'Unordered' | ID;