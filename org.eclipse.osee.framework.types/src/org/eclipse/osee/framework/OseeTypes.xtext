grammar org.eclipse.osee.framework.OseeTypes with org.eclipse.xtext.common.Terminals

generate oseeTypes "http://www.eclipse.org/osee/framework/OseeTypes"


OseeTypeModel :
	(imports+=Import)*
	(types+=OseeType)*;

Import :
	'import' importURI=STRING;
	
terminal WHOLE_NUM_STR: ('0'..'9')+;

QUALIFIED_NAME : ID ('.' ID)*;

OseeType:
	ArtifactType | RelationType | AttributeType | OseeEnumType;

ArtifactType:
	'abstract'? 'artifactType' name=STRING ('extends' superArtifactType=[ArtifactType|STRING])? '{'
	    (validAttributeTypes+=(AttributeTypeRef))*
	'}';

AttributeTypeRef:
	'attribute' validAttributeType=[AttributeType|STRING];
	
AttributeType:
		'attributeType' name=STRING ('extends' baseAttributeType=AttributeBaseType) '{'
		'dataProvider' dataProvider = ('DefaultAttributeDataProvider' | 'UriAttributeDataProvider' | 'MappedAttributeDataProvider' | QUALIFIED_NAME)
		'min' min=WHOLE_NUM_STR
		'max' max=(WHOLE_NUM_STR | 'unlimited')
		('taggerId' taggerId = ('DefaultAttributeTaggerProvider' | QUALIFIED_NAME))?
		('enumType' enumType = [OseeEnumType|STRING])?
		('description' description = STRING)?
		('defaultValue' defaultValue = STRING)?
		('fileExtension' fileExtension = STRING)?
	'}';

AttributeBaseType:
	'BooleanAttribute' | 'CompressedContentAttribute' | 'DateAttribute' | 'EnumeratedAttribute' | 'FloatingPointAttribute' | 'IntegerAttribute' | 'JavaObjectAttribute' | 'StringAttribute' | 'WordAttribute' | QUALIFIED_NAME;

OseeEnumType:
	'oseeEnumType' name=STRING '{' 
		(enumEntries+=OseeEnumEntry)*
	'}';

OseeEnumEntry:
	'entry' name=STRING (ordinal=WHOLE_NUM_STR)?;

enum RelationMultiplicityEnum :  
ONE_TO_MANY | MANY_TO_MANY | MANY_TO_ONE | ONE_TO_ONE
;

RelationType:
	'relationType' name=STRING '{'
		'sideAName' sideAName=STRING
		'sideAArtifactType' sideAArtifactType=[ArtifactType|STRING]
		'sideBName' sideBName=STRING
		'sideBArtifactType' sideBArtifactType=[ArtifactType|STRING]
		'defaultOrderType' defaultOrderType=('Lexicographical_Ascending' | 'Lexicographical_Descending' | 'Unordered' | ID )
		'multiplicity' multiplicity=RelationMultiplicityEnum
	'}';