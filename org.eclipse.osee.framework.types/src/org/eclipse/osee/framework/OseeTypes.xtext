grammar org.eclipse.osee.framework.OseeTypes with org.eclipse.xtext.common.Terminals

generate oseeTypes "http://www.eclipse.org/osee/framework/OseeTypes"

Model :
	(imports+=Import)*
	(types+=OseeType)*;

Import :
	'import' importURI=STRING;

QUALIFIED_NAME : ID ('.' ID)*;
DIGITS : ('0'|'1'|'2'|'3'|'4'|'5'|'6'|'7'|'8'|'9')+;

OseeType:
	ArtifactType | RelationType | AttributeType | OseeEnumType;

ArtifactType:
	'abstract'? 'artifactType' name=QUALIFIED_NAME ('extends' superArtifactType=[ArtifactType])? '{'
	    (validTypes+=(AttributeTypeRef | RelationTypeRef))*
	'}';

AttributeTypeRef:
	'attribute' validAttributeType=[AttributeType];
	
RelationTypeRef:
	'relation' validRelationType=[RelationType];

AttributeType:
		'attributeType' name=QUALIFIED_NAME ('extends' baseAttributeType=AttributeBaseType) '{'
		'dataProvider' dataProvider = ('DefaultAttributeDataProvider' | 'UriAttributeDataProvider' | 'MappedAttributeDataProvider' | QUALIFIED_NAME)
		'min' min=DIGITS
		'max' max=(DIGITS | 'unlimited')
		('taggerId' taggerId = ('DefaultAttributeTaggerProvider' | QUALIFIED_NAME))?
		('enumType' enumType = [OseeEnumType])?
		('description' description = STRING)?
		('defaultValue' defaultValue = STRING)?
		('fileExtension' fileExtension = STRING)?
	'}';

AttributeBaseType:
	'BooleanAttribute' | 'CompressedContentAttribute' | 'DateAttribute' | 'EnumeratedAttribute' | 'FloatingPointAttribute' | 'IntegerAttribute' | 'JavaObjectAttribute' | 'StringAttribute' | 'WordAttribute' | QUALIFIED_NAME;

OseeEnumType:
	'oseeEnumType' name=QUALIFIED_NAME '{' 
		(enums+=OseeEnum)*
	'}';

OseeEnum:
	name=STRING (ordinal=DIGITS)?;

RelationType:
	'relationType' name=QUALIFIED_NAME '{'
		'sideAName' sideAName=STRING
		'sideAArtifactType' sideAArtifactType=[ArtifactType]
		'sideBName' sideBName=STRING
		'sideBArtifactType' sideBArtifactType=[ArtifactType]
		'defaultOrderType' defaultOrderType=('Lexicographical_Ascending' | 'Lexicographical_Descending' | 'Unordered' | ID )
		'multiplicity' multiplicity=('one-to-many' | 'many-to-many' | 'many-to-one')
	'}';